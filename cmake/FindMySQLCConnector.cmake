# Look for MySQL C Connector
# Once done, this will define
#
#  MYSQLCCONNECTOR_FOUND - system has MySQL-C-Connector
#  MYSQLCCONNECTOR_INCLUDE_DIR - MySQL-C-Connector include directories
#  MYSQLCCONNECTOR_LIBRARY - MySQL-C-Connector library
#  MYSQLCCONNECTOR_LIBRARY_STATIC - MySQL-C-Connector static library (Linux only)
#
# The user may wish to set, in the CMake GUI or otherwise, this variable:
#  MYSQLCCONNECTOR_ROOT_DIR - path to start searching for the module

set(MYSQLCCONNECTOR_ROOT_DIR
	"${MYSQLCCONNECTOR_ROOT_DIR}"
	CACHE
	PATH
	"Where to start looking for this component."
)

if(WIN32)
    find_path(
		MYSQLCCONNECTOR_INCLUDE_DIR
		NAMES
		"mysql_version.h"
		HINTS
		${MYSQLCCONNECTOR_ROOT_DIR}
		PATH_SUFFIXES
		include
	)

    find_library(
		MYSQLCCONNECTOR_LIBRARY
		NAMES
		"libmysql.lib"
		HINTS
		${MYSQLCCONNECTOR_ROOT_DIR}
		PATH_SUFFIXES
		lib
	)
else()
    find_path(
		MYSQLCCONNECTOR_INCLUDE_DIR
		NAMES
		"mysql_version.h"
		PATHS
		"/usr/include"
		HINTS
		${MYSQLCCONNECTOR_ROOT_DIR}
		PATH_SUFFIXES
		mysql
	)

    find_library(
		MYSQLCCONNECTOR_LIBRARY
		NAME
		"libmysqlclient_r.so"
		HINTS
		${MYSQLCCONNECTOR_ROOT_DIR}
	)
	
    find_library(
		MYSQLCCONNECTOR_LIBRARY_STATIC
		NAME
		"libmysqlclient.a"
		HINTS
		${MYSQLCCONNECTOR_ROOT_DIR}
	)
endif()

if(MYSQLCCONNECTOR_INCLUDE_DIR)
	set(_mysqlcc_VERSION_REGEX "#define MYSQL_VERSION_ID[ \t]+([0-9]+)")
	file(
		STRINGS 
		"${MYSQLCCONNECTOR_INCLUDE_DIR}/mysql_version.h" 
		_mysqlcc_VERSION_H_CONTENTS 
		REGEX "${_mysqlcc_VERSION_REGEX}"
	)
	if(_mysqlcc_VERSION_H_CONTENTS)
		string(REGEX MATCH "${_mysqlcc_VERSION_REGEX}"
			MYSQLCCONNECTOR_VERSION_ID "${_mysqlcc_VERSION_H_CONTENTS}"
		)
		set(MYSQLCCONNECTOR_VERSION_ID "${CMAKE_MATCH_1}")
		math(EXPR 
			MYSQLCCONNECTOR_VERSION_MAJOR
			"${MYSQLCCONNECTOR_VERSION_ID} / 10000"
		)
		math(EXPR 
			MYSQLCCONNECTOR_VERSION_MINOR
			"${MYSQLCCONNECTOR_VERSION_ID} / 100 % 100"
		)
		math(EXPR 
			MYSQLCCONNECTOR_VERSION_PATCH
			"${MYSQLCCONNECTOR_VERSION_ID} % 100"
		)
		set(MYSQLCCONNECTOR_VERSION ${MYSQLCCONNECTOR_VERSION_MAJOR})
		set(MYSQLCCONNECTOR_VERSION ${MYSQLCCONNECTOR_VERSION}.${MYSQLCCONNECTOR_VERSION_MINOR})
		set(MYSQLCCONNECTOR_VERSION ${MYSQLCCONNECTOR_VERSION}.${MYSQLCCONNECTOR_VERSION_PATCH})
	endif()
endif()

mark_as_advanced(
	MYSQLCCONNECTOR_INCLUDE_DIR
	MYSQLCCONNECTOR_LIBRARY
	MYSQLCCONNECTOR_LIBRARY_STATIC
)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
	MySQLCConnector
	REQUIRED_VARS
		MYSQLCCONNECTOR_INCLUDE_DIR
		MYSQLCCONNECTOR_LIBRARY
		MYSQLCCONNECTOR_VERSION
	VERSION_VAR
		MYSQLCCONNECTOR_VERSION
)

if(MYSQLCCONNECTOR_FOUND)
    mark_as_advanced(MYSQLCCONNECTOR_ROOT_DIR)
endif() 
